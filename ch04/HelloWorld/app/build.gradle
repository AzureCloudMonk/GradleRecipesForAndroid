apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.kousenit.helloworld"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        arrogant {
            applicationId 'com.kousenit.helloworld.arrg'
        }
        friendly {
            applicationId 'com.kousenit.helloworld.frnd'
        }
        obsequious {
            applicationId 'com.kousenit.helloworld.obsq'
        }
    }

    adbOptions {
        timeOutInMs = 60 * 1000
    }
}

gradle.taskGraph.whenReady { graph ->
    if (project.hasProperty('noLint')) {
        graph.allTasks.findAll { it.name ==~ /lint.*/ }*.enabled = false
    }
}

task installDebugFlavors {
    description 'install all the debug variants on all connected devices'
    android.applicationVariants.all { v ->
        if (v.name.endsWith('Debug')) {
            String name = v.name.capitalize()
            dependsOn "install$name"
        }
    }
}

task printVariantNames {
    description 'print the names of all the variants'
    doLast {
        android.applicationVariants.all { variant ->
            println variant.name
        }
    }
}

task copyApks(type: Copy) {
    dependsOn 'assembleDebug'
    description "copy apks to the 'apk' directory in the root project"
    from("$buildDir/outputs/apk") {
        exclude '**/*unsigned.apk', '**/*unaligned.apk'
    }
    into '../apks'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.3.1'
}
